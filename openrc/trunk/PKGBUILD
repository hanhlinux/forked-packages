# Maintainer: artoo <artoo@artixlinux.org>
# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Contributor: williamh <williamh@gentoo.org>

_ever=1.1
_eurl="https://gitea.artixlinux.org/artix/openrc-extra/archive"
_aurl="https://gitea.artixlinux.org/artix/alpm-hooks"
_commit=3dc7ecf3fb340314a2ada79fe2f1dde738e6ede7

pkgbase=openrc
pkgname=('openrc' 'openrc-bash-completions' 'openrc-zsh-completions' 'openrc-deptree2dot')
pkgver=0.44.4
pkgrel=1
pkgdesc="Gentoo's universal init system"
arch=('x86_64')
url="https://github.com/OpenRC/openrc"
license=('BSD2')
makedepends=('psmisc' 'netifrc' 'zsh' 'perl' 'git')
source=("${pkgbase}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${pkgbase}-extra-${_ever}.tar.gz::${_eurl}/${_ever}.tar.gz"
        "git+${_aurl}.git#commit=${_commit}"
        'openrc.logrotate'
        'sysctl.conf'
        'artix-rc-conf.patch'
        )

sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare(){
    cd "${pkgbase}-${pkgver}"
    patch -Np 1 -i ../artix-rc-conf.patch
}

check(){
     cd $srcdir/$pkgbase-$pkgver
     make -k check
}

build(){
    local _make_opt=( 
    BRANDING="Hanh Linux"
    SYSCONFDIR=/etc
    PREFIX=/usr
    SBINDIR=/usr/bin
    LIBEXECDIR=/usr/lib/openrc
    MKSELINUX=no
    MKPAM=pam
    MKTERMCAP=ncurses
    MKNET=no
    MKSYSVINIT=yes
    MKBASHCOMP=yes
    MKZSHCOMP=yes
    MKPKGCONFIG=yes
    )

cd $srcdir/$pkgbase-$pkgver
make "${_make_opt[@]}"
    }

package_openrc() {
    depends=('psmisc' 'netifrc' 'inetutils')
    optdepends=('networkmanager-openrc: networkmanager init script'
                'elogind-openrc: elogind init script'
                'openrc-deptree2dot: Graph the Dependency Tree')
    backup=('etc/rc.conf'
            'etc/conf.d/consolefont'
            'etc/conf.d/keymaps'
            'etc/conf.d/hostname'
            'etc/conf.d/modules'
            'etc/conf.d/hwclock'
            'etc/conf.d/opentmpfiles-dev'
            'etc/conf.d/opentmpfiles-setup'
            'etc/conf.d/udev'
            'etc/conf.d/udev-trigger'
            'etc/conf.d/udev-settle'
            etc/conf.d/agetty.tty{1,2,3,4,5,6})
    provides=('svc-manager' 'init-rc' 'librc.so' 'libeinfo.so')
    conflicts=('svc-manager' 'init-rc')
    cd $srcdir/$pkgbase-$pkgver
   CFLAGS="-fPIC -lpam" make DESTDIR="${pkgdir}" "${_make_opt[@]}" install

    install -Dm644 "${srcdir}/${pkgbase}".logrotate "${pkgdir}"/etc/logrotate.d/"${pkgbase}"

    install -d "${pkgdir}"/usr/lib/{openrc/cache,binfmt.d,sysctl.d}

    # sysctl defaults
    install -m755 "${srcdir}"/sysctl.conf "${pkgdir}"/usr/lib/sysctl.d/50-default.conf

    # pacman hooks
    cd ${srcdir}/alpm-hooks
    make DESTDIR="${pkgdir}" install_openrc

    # openrc extra; agetty,kmod,udev,tmpfiles,sysusers
    cd "${srcdir}/${pkgbase}-extra"
    make DESTDIR="${pkgdir}" install

    # license
    install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/LICENSE "${pkgdir}"/usr/share/licenses/"${pkgbase}"/LICENSE
}

package_openrc-bash-completions() {
    pkgdesc="OpenRC completions for Bash"

    cd ${srcdir}/${pkgbase}-${pkgver}/bash-completion
    make DESTDIR=${pkgdir} install
}

package_openrc-zsh-completions() {
    pkgdesc="OpenRC completions for ZSH"

    cd ${srcdir}/${pkgbase}-${pkgver}/zsh-completion
    make DESTDIR=${pkgdir} install
}

package_openrc-deptree2dot() {
    pkgdesc="Graph the OpenRC Dependency Tree"
    depends=('perl')

    cd "${pkgbase}-${pkgver}"
    install -Dm755 support/deptree2dot/deptree2dot "${pkgdir}"/usr/bin/deptree2dot
}
